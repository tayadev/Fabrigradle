plugins {
    id 'java-gradle-plugin'
    id 'io.freefair.lombok' version '6.5.0.2'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '1.0.0'
}

group 'one.taya'
version '4.0.0'

pluginBundle {
    website = 'https://github.com/tayadev/fabrigradle' 
    vcsUrl = 'https://github.com/tayadev/fabrigradle' 
    tags = ['fabricmc', 'minecraft']     
}

repositories {
    maven {
        name = 'Fabric'
        url = 'https://maven.fabricmc.net'
    }
    mavenCentral()
}

dependencies {
    implementation 'fabric-loom:fabric-loom.gradle.plugin:0.13.6'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.0'
    implementation 'commons-io:commons-io:2.11.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    testImplementation 'org.skyscreamer:jsonassert:1.5.1'
}

gradlePlugin {
    plugins {
        fabrigradle {
            id = 'one.taya.fabrigradle'
            displayName = 'Fabrigradle'
            description = 'An intelligent Gradle plugin used to develop mods for fabric'
            implementationClass = 'one.taya.fabrigradle.FabrigradlePlugin'
        }
    }
}

sourceSets {
    functionalTest {
    }
}

configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

tasks.register('functionalTest', Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    useJUnitPlatform()
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)

tasks.named('check') {
    dependsOn(tasks.functionalTest)
}

tasks.named('test') {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'fabrigradle'
            from components.java
        }
    }
}